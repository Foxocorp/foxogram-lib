@PACKAGE_INIT@

macro(import_targets type)
    if(NOT EXISTS "${CMAKE_CURRENT_LIST_DIR}/foxogram_lib-${type}-targets.cmake")
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE "foxogram_lib ${type} libraries were requested but not found")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND OFF)
        return()
    endif()

    include("${CMAKE_CURRENT_LIST_DIR}/foxogram_lib-${type}-targets.cmake")
endmacro()

if(NOT TARGET foxogram_lib::foxogram_lib)
    set(type "")

    if(DEFINED FOXOGRAM_LIB_SHARED_LIBS)
        if(FOXOGRAM_LIB_SHARED_LIBS)
            set(type "shared")
        else()
            set(type "static")
        endif()
    elseif(BUILD_SHARED_LIBS AND EXISTS "${CMAKE_CURRENT_LIST_DIR}/foxogram_lib-shared-targets.cmake")
        set(type "shared")
    elseif(EXISTS "${CMAKE_CURRENT_LIST_DIR}/foxogram_lib-static-targets.cmake")
        set(type "static")
    else()
        set(type "shared")
    endif()

    import_targets(${type})
    check_required_components(foxogram_lib)
    message("-- Found ${type} foxogram_lib (version ${${CMAKE_FIND_PACKAGE_NAME}_VERSION})")
endif()
