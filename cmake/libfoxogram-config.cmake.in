@PACKAGE_INIT@

macro(import_targets type)
    if(NOT EXISTS "${CMAKE_CURRENT_LIST_DIR}/libfoxogram-${type}-targets.cmake")
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE "libfoxogram ${type} libraries were requested but not found")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND OFF)
        return()
    endif()

    include("${CMAKE_CURRENT_LIST_DIR}/libfoxogram-${type}-targets.cmake")
endmacro()

if(NOT TARGET libfoxogram::libfoxogram)
    set(type "")

    if(DEFINED LIBFOXOGRAM_SHARED_LIBS)
        if(LIBFOXOGRAM_SHARED_LIBS)
            set(type "shared")
        else()
            set(type "static")
        endif()
    elseif(BUILD_SHARED_LIBS AND EXISTS "${CMAKE_CURRENT_LIST_DIR}/libfoxogram-shared-targets.cmake")
        set(type "shared")
    elseif(EXISTS "${CMAKE_CURRENT_LIST_DIR}/libfoxogram-static-targets.cmake")
        set(type "static")
    else()
        set(type "shared")
    endif()

    import_targets(${type})
    check_required_components(libfoxogram)
    message("-- Found ${type} libfoxogram (version ${${CMAKE_FIND_PACKAGE_NAME}_VERSION})")
endif()
